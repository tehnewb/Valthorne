plugins {
    id 'java'
    id 'com.vanniktech.maven.publish' version '0.36.0'
}

group = "io.github.tehnewb"
version = "1.0.0"
description = "A 2D game development library"

repositories {
    mavenCentral()
}

def lwjglVersion = "3.3.4"
def lwjglNatives = "natives-windows"

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-openal"

    implementation "com.googlecode.soundlibs:mp3spi:1.9.5-1"
    implementation "com.googlecode.soundlibs:tritonus-share:0.3.7-2"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(23) }
    withSourcesJar()
    withJavadocJar() // keep the standard javadoc jar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:none", "-quiet")
    options.addStringOption("encoding", "UTF-8")
    options.addBooleanOption("html5", true)
    failOnError = false
}

/**
 * The Vanniktech plugin often creates an extra "plainJavadocJar" which conflicts
 * with java.withJavadocJar() (both publish classifier "javadoc").
 * We disable the extra one to avoid: multiple artifacts with identical extension/classifier.
 */
tasks.matching { it.name == "plainJavadocJar" }.configureEach {
    enabled = false
}

mavenPublishing {
    coordinates(group.toString(), project.name.toString(), version.toString())
    pom {
        name = "Valthorne"
        description = "2d game library"
        inceptionYear = "2025"
        url = "https://github.com/tehnewb/Valthorne"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "tehnewb"
                name = "Albert Beaupre"
                url = "https://github.com/tehnewb/"
            }
        }

        scm {
            url = "https://github.com/tehnewb/Valthorne"
            connection = "scm:git:https://github.com/tehnewb/Valthorne.git"
            developerConnection = "scm:git:ssh://git@github.com/tehnewb/Valthorne.git"
        }
    }
}
