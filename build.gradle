plugins {
    id 'java'
    id 'com.vanniktech.maven.publish' version '0.36.0'
}

group = "io.github.tehnewb"
version = "1.0.0"
description = "A 2D game development library"

repositories {
    mavenCentral()
}

def lwjglVersion = "3.3.4"
def os = org.gradle.internal.os.OperatingSystem.current()
def arch = System.getProperty("os.arch").toLowerCase(Locale.ROOT)
def lwjglNatives

if (os.isWindows()) {
    lwjglNatives = "natives-windows"
} else if (os.isMacOsX()) {
    lwjglNatives = arch.contains("aarch64") || arch.contains("arm64") ? "natives-macos-arm64" : "natives-macos"
} else {
    if (arch.contains("aarch64") || arch.contains("arm64")) {
        lwjglNatives = "natives-linux-arm64"
    } else if (arch.contains("arm")) {
        lwjglNatives = "natives-linux-arm32"
    } else {
        lwjglNatives = "natives-linux"
    }
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-openal"

    implementation "com.googlecode.soundlibs:mp3spi:1.9.5-1"
    implementation "com.googlecode.soundlibs:tritonus-share:0.3.7-2"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(23) }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:none", "-quiet")
    options.addStringOption("encoding", "UTF-8")
    options.addBooleanOption("html5", true)
    failOnError = false
}

tasks.matching { it.name == "plainJavadocJar" }.configureEach {
    enabled = false
}

mavenPublishing {
    coordinates(group.toString(), project.name.toString(), version.toString())
    pom {
        name = "Valthorne"
        description = "2D game library"
        inceptionYear = "2025"
        url = "https://github.com/tehnewb/Valthorne"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "tehnewb"
                name = "Albert Beaupre"
                url = "https://github.com/tehnewb/"
            }
        }

        scm {
            url = "https://github.com/tehnewb/Valthorne"
            connection = "scm:git:https://github.com/tehnewb/Valthorne.git"
            developerConnection = "scm:git:ssh://git@github.com/tehnewb/Valthorne.git"
        }
    }
}
